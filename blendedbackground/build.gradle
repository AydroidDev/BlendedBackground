apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def currentVersion = '1.0.2'
def webUrl = 'https://github.com/alxgrk/blendedbackground'
def gitUrl = 'https://github.com/alxgrk/blendedbackground.git'

group = 'com.github.alxgrk.blendedbackground'
version = currentVersion

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName currentVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:palette-v7:24.2.1'
    compile 'com.android.support:support-annotations:24.2.1'

    provided "org.projectlombok:lombok:1.12.6"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                name "blendedbackground"
                description = 'A smooth color transition for the background of your Android App.'
                url webUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'alxgrk'
                        name 'Alexander Girke'
                        email 'alexgirke@web.de'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url webUrl
                }
            }
        }
    }
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = 'blended-background'
        name = group
        userOrg = 'alxgrk-ag'

        version {
            name = currentVersion
            desc = 'Now supports radial gradients.'
            vcsTag = currentVersion
            gpg {
                sign = true
            }
        }

        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        websiteUrl = webUrl
    }
    configurations = ['archives']
}

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}
